# Copyright (c) 2008-2009, Intel Corporation.
# Copyright (c) 2006-2007, The Trustees of Stanford University.
# All rights reserved.

# Author: Mayur Naik (mhn@cs.stanford.edu)
# name=flowins-thresc-dlog

.include "M.dom"
.include "V.dom"
.include "C.dom"
.include "P.dom"
.include "Z.dom"
.include "F.dom"
.include "E.dom"

.bddvarorder F0_E0_M0xP0_V0_C0xC1_Z0

###
# Relations
###

CVC(c1:C,v:V,c2:C) input
FC(f:F,c:C) input
CFC(c1:C,f:F,c2:C) input
MmethArg(m:M,z:Z,v:V) input
escO(o:C) output
MV(m:M,v:V) input
MP(m:M,p:P) input
EV(e:E,v:V) input
escCPVO(c:C,p:P,v:V,o:C) output

visitedE(e:E) output
escCE(c:C,e:E) output
escE(e:E) output

###
# Constraints
###

escO(o) :- FC(_,o).
escO(o) :- MmethArg(1,0,v), CVC(_,v,o).
escO(o2) :- CFC(o1,_,o2), escO(o1).

escCPVO(c,p,v,o) :- CVC(c,v,o), MV(m,v), MP(m,p), escO(o).
escCE(c,e) :- EV(e,v), CVC(c,v,o), escO(o).

visitedE(e) :- EV(e,v), CVC(_,v,_).
escE(e) :- escCE(_,e).
